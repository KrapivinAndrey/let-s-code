#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

Функция ПолучитьРезультатыТестирования(Код, НаборТестов) Экспорт
	
	ЗаписьЖурналаРегистрации("ПроверкаКода", УровеньЖурналаРегистрации.Информация, Справочники.ИТК_Задачи, , Код);
	
	Результат = Новый Структура;
	ВыполненУспешно = Ложь;        
	Отладка = Новый Массив;
	Индекс = 0;
	
	Протокол = "<html><head></head><body><font size=""+2"">";
				
	Для Каждого СтрокаТест Из НаборТестов Цикл
		
		Индекс = Индекс + 1;
		ТестВыполненУспешно = Ложь;
		РезультатТеста = РезультатВыполненияТеста(Код, СтрокаТест.Тест, СтрокаТест.Предустановка);
		Попытка
			
			ТестВыполненУспешно = РезультатТеста.РезультатПроверки;
			ВыводТекста = РезультатТеста.ВыводТекста;
			Дебаг = РезультатТеста.Отладка;
			
		Исключение
			
			ТестВыполненУспешно = Ложь;        
			ВыводТекста = "";
			Дебаг = "";
			
		КонецПопытки;
		
		Протокол = Протокол + СтрШаблон("<p><code> <b>Тест №%1 </b>: %2 : %3 :<i> %4 </i> </code></p>",
									Индекс,
									?(ТестВыполненУспешно, "<font color=""green"">Пройден</font>", 
										"<font color=""red"">Ошибка</font>"),	
									?(ЗначениеЗаполнено(СтрокаТест.Представление), СтрокаТест.Представление, СтрокаТест.Тест),
									ВыводТекста);
									
								
		Отладка.Добавить("Тест " + Индекс);
		Отладка.Добавить("------");
		Отладка.Добавить(Дебаг);
		Отладка.Добавить("");
									
		Если НЕ ТестВыполненУспешно Тогда
			ВыполненУспешно = Ложь;
			Прервать;
		КонецЕсли;
		
		ВыполненУспешно = Истина;
		
	КонецЦикла;
	
	Протокол = Протокол + "</font></body></html>";
	
	Результат.Вставить("ВыполненУспешно", ВыполненУспешно);
	Результат.Вставить("ПротоколТестирования", Протокол);
	Результат.Вставить("Отладка", СтрСоединить(Отладка, Символы.ПС));
	Возврат Результат;
	
КонецФункции 

#КонецОбласти

#Область СлужебныеФункции

Функция РезультатВыполненияТеста(Код, Тест, Предустановка) 
	
	РезультатПроверки = Ложь;
	КодДляПроверки = СокрЛП(Код) + Символы.ПС;
		
	КодТеста = СтрШаблон("Результат = %1;", СокрЛП(Тест));
	ФайлОтладка = ПолучитьИмяВременногоФайла("txt");
	Шаблон = ПолучитьОбщийМакет("ДевБаттл_Оскрипт").ПолучитьТекст();

	Скрипт = СтрШаблон(Шаблон
						, КодДляПроверки
						, Предустановка
						, КодТеста
						, ФайлОтладка);	
	
	Протокол = "";         
	
	РезультатПроверки = ИТК_ЗадачиТестированияПривилегированный.ВыполнитьСкрипт(Скрипт, Протокол);	
	Отладка = РезультатОтладки(ФайлОтладка);
	
	Возврат НовыйРезультатПроверки(РезультатПроверки, Протокол, Отладка);
		
КонецФункции                 

Функция НовыйРезультатПроверки(РезультатПроверки, ВыводТекста, Отладка)
	
	Возврат Новый Структура("РезультатПроверки, ВыводТекста, Отладка", РезультатПроверки, ВыводТекста, Отладка);
	
КонецФункции        

Функция РезультатОтладки(ПутьКФайлу)
	
	Файл = Новый Файл(ПутьКФайлу);
	Если Файл.Существует() Тогда
		
		Чтение = Новый ЧтениеТекста(ПутьКФайлу);
		Отладка = Чтение.Прочитать();
		Попытка
			
			УдалитьФайлы(ПутьКФайлу);
			
		Исключение
		КонецПопытки;
		
	Иначе
		
		Отладка = "";
		
	КонецЕсли;
	
	Возврат Отладка;
	
КонецФункции

#КонецОбласти

#КонецЕсли