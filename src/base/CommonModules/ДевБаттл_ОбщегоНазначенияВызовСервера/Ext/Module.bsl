
Процедура ИнициализацияПользователей() Экспорт
	Если ЗначениеЗаполнено(ИмяПользователя()) 
			И Не РольДоступна("ПолныеПрава") Тогда
		
		Возврат;
		
	КонецЕсли;
	// Инициализация пользователей
	
	// Администратор
	ПроверитьСоздатьПользователя("Администратор", "Администратор", "ПолныеПрава");
	
	// Игроки
	ПроверитьСоздатьПользователя("Игрок1", "Первый игрок", 		"ДевБаттл_Игрок");
	ПроверитьСоздатьПользователя("Игрок2", "Второй игрок", 		"ДевБаттл_Игрок");
	ПроверитьСоздатьПользователя("Игрок3", "Третий игрок", 		"ДевБаттл_Игрок");
	ПроверитьСоздатьПользователя("Игрок4", "Четвертый игрок", 	"ДевБаттл_Игрок");
	ПроверитьСоздатьПользователя("Игрок5", "Пятый игрок", 		"ДевБаттл_Игрок");
	ПроверитьСоздатьПользователя("Игрок6", "Шестой игрок", 		"ДевБаттл_Игрок");

	
КонецПроцедуры

Процедура ПроверитьСоздатьПользователя(Имя, ПолноеИмя, Роль)
	
	Если ПользователиИнформационнойБазы.НайтиПоИмени(Имя) = Неопределено Тогда
		
		Юзер = ПользователиИнформационнойБазы.СоздатьПользователя();
		Юзер.Имя = Имя;
		Юзер.ПолноеИмя = ПолноеИмя;
		Юзер.Роли.Добавить(Метаданные.Роли.Найти(Роль));
		Юзер.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗафиксироватьРезультатИспытания(ИмяИгрока, Выполнено, ПотраченоВремени) Экспорт
	
	Запись = РегистрыСведений.ДевБаттл_Результаты.СоздатьМенеджерЗаписи();
	
	Запись.Игрок = ИмяИгрока;
	Запись.НомерРаунда = Константы.ДевБаттл_Раунд.Получить();
	
	Запись.Выполнено = Выполнено;
	Запись.ПотраченоВремени = ПотраченоВремени;
	
	Запись.Записать(Истина);	          
	
	Запись = РегистрыСведений.ДевБаттл_ТекущееСостояниеИгрока.СоздатьМенеджерЗаписи();
	Запись.Игрок = ИмяИгрока;
	Запись.Состояние = Перечисления.ДевБаттл_СостояниеИгрока.РезультатИспытания;
	Запись.Записать(Истина);	
	
КонецПроцедуры

Функция ПацанскаяЦитата() Экспорт
	
	Цитатник = ПолучитьОбщийМакет("ДевБаттл_Цитатник");
	Высота = Цитатник.ВысотаТаблицы;
	
	ГСЧ = Новый ГенераторСлучайныхЧисел(ТекущаяУниверсальнаяДатаВМиллисекундах());
	Номер = ГСЧ.СлучайноеЧисло(1, Высота);
	
	Возврат Цитатник.Область(Номер, 1).Текст;
	
КонецФункции