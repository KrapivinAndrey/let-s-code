Функция РезультатыИспытаний(Игрок = Неопределено) Экспорт
	
	СхемаКомпоновкиДанных = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	
	НастройкиПоУмолчанию = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	Если Игрок <> Неопределено Тогда
		
		ЭлементыОтбора = НастройкиПоУмолчанию.Отбор.Элементы;
		
		ЭлементОтбораКомпоновкиДанных = ЭлементыОтбора.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораКомпоновкиДанных.Использование		= Истина;
		ЭлементОтбораКомпоновкиДанных.ЛевоеЗначение		= Новый ПолеКомпоновкиДанных("Игрок");
		ЭлементОтбораКомпоновкиДанных.ВидСравнения		= ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбораКомпоновкиДанных.ПравоеЗначение	= Игрок;
		
	КонецЕсли;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, 
						НастройкиПоУмолчанию,,,
						Тип("ГенераторМакетаКомпоновкиДанных"));
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки,,,Истина);

	РезультатИспытаний = Новый ТабличныйДокумент;
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
 	ПроцессорВывода.УстановитьДокумент(РезультатИспытаний);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);	  
	
	Возврат РезультатИспытаний;
	
КонецФункции