#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

Функция ПолучитьРезультатыТестирования(Код, НаборТестов) Экспорт
	
	Результат = Новый Структура;
	ВыполненУспешно = Ложь;
	Индекс = 0;
	
	Протокол = "<html><head></head><body>";
				
	Для Каждого СтрокаТест Из НаборТестов Цикл
		
		Индекс = Индекс + 1;
		ТестВыполненУспешно = Ложь;
		РезультатТеста = РезультатВыполненияТеста(Код, СтрокаТест.Тест, СтрокаТест.Предустановка);
		Попытка
			ТестВыполненУспешно = Булево(РезультатТеста); 
		Исключение
			ТестВыполненУспешно = Ложь;
		КонецПопытки;
		
		Протокол = Протокол + СтрШаблон("<p><code> <b>Тест №%1 </b>: %2 : %3 :<i> %4 </i> </code></p>",
									Индекс,
									?(ТестВыполненУспешно, "<font color=""green"">Пройден</font>", 
										"<font color=""red"">Ошибка</font>"),	
									?(ЗначениеЗаполнено(СтрокаТест.Представление), СтрокаТест.Представление, СтрокаТест.Тест),
									РезультатТеста);
									
		Если НЕ ТестВыполненУспешно Тогда
			ВыполненУспешно = Ложь;
			Прервать;
		КонецЕсли;
		
		ВыполненУспешно = Истина;
		
	КонецЦикла;
	
	Протокол = Протокол + "</body></html>";
	
	Результат.Вставить("ВыполненУспешно", ВыполненУспешно);
	Результат.Вставить("ПротоколТестирования", Протокол);
	Возврат Результат;
	
КонецФункции 

#КонецОбласти

#Область СлужебныеФункции

Функция РезультатВыполненияТеста(Код, Тест, Предустановка) 
	
	РезультатПроверки = Ложь;
	КодДляПроверки = СокрЛП(Код) + Символы.ПС;
		
	КодТеста = СтрШаблон("Результат = %1;", СокрЛП(Тест));
	Скрипт = СтрШаблон( 
		"%1
		|Попытка
		|	%2
		|	%3
		|Исключение
		|	Результат = ОписаниеОшибки();
		|КонецПопытки;
		|Сообщить(Результат);", КодДляПроверки, Предустановка, КодТеста);	
	
	РезультатПроверки = ИТК_ЗадачиТестированияПривилегированный.ВыполнитьСкрипт(Скрипт);	
	Возврат РезультатПроверки;
		
КонецФункции

#КонецОбласти

#КонецЕсли