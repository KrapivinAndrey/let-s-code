#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Фунцкция осуществялет тестирования кода по заданному набору тестов и формирует протокол
// Параметры:
//		Код - Строка - Код для проверки
//		НаборТестов - ТаблицаЗначений - Таблица тестов для проверки
//	Возвращаемое значение:
//		Структура - СоставСтруктуры[ПротоколТестирования: протокол в формате HTML; ВыполненУспешно: Признак успешного выполнения теста]
//
Функция РезультатыТестирования(Код, НаборТестов) Экспорт
	
	Результат = Новый Структура;
	ВыполненУспешно = Ложь;
	Индекс = 0;
	
	Протокол = Новый Массив;
	Протокол.Добавить("<html><head></head><body>");
				
	Для Каждого СтрокаТест Из НаборТестов Цикл
		
		Индекс = Индекс + 1;
		ТестВыполненУспешно = Ложь;
		РезультатТеста = РезультатВыполненияТеста(Код, СтрокаТест.Тест, СтрокаТест.Предустановка);
		Попытка
			ТестВыполненУспешно = Булево(РезультатТеста); 
		Исключение
			ТестВыполненУспешно = Ложь;
		КонецПопытки; 
		    
		ПредставлениеРезультата = ?(ТестВыполненУспешно, "<font color=""green"">Пройден</font>", 
			"<font color=""red"">Ошибка</font>");  
		
		ПредставлениеВывода = ?(ЗначениеЗаполнено(СтрокаТест.Представление), СтрокаТест.Представление, СтрокаТест.Тест);
		
		РезультатРазметка = СтрШаблон("<p><code> <b>Тест №%1 </b>: %2 : %3 :<i> %4 </i> </code></p>",
								Индекс, 
								ПредставлениеРезультата,
								ПредставлениеВывода,
								РезультатТеста);
									
		Протокол.Добавить(РезультатРазметка);
									
		Если НЕ ТестВыполненУспешно Тогда
			ВыполненУспешно = Ложь;
			Прервать;
		КонецЕсли;
		
		ВыполненУспешно = Истина;
		
	КонецЦикла;
	
	Протокол.Добавить("</body></html>");
	ПротоколПредставление = СтрСоединить(Протокол);
	
	Результат.Вставить("ВыполненУспешно", ВыполненУспешно);
	Результат.Вставить("ПротоколТестирования", ПротоколПредставление);  
	
	Возврат Результат;
	
КонецФункции 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РезультатВыполненияТеста(Код, Тест, Предустановка) 
	
	РезультатПроверки = Ложь;
	КодДляПроверки = СокрЛП(Код) + Символы.ПС;
		
	КодТеста = СтрШаблон("Результат = %1;", СокрЛП(Тест));
	Скрипт = СтрШаблон( 
		"%1
		|Попытка
		|	%2
		|	%3
		|Исключение
		|	Результат = ОписаниеОшибки();
		|КонецПопытки;
		|Сообщить(Результат);", КодДляПроверки, Предустановка, КодТеста);	
	
	РезультатПроверки = ИТК_ЗадачиТестированияПолныеПрава.ВыполнитьСкрипт(Скрипт);	
	Возврат РезультатПроверки;
		
КонецФункции

#КонецОбласти

#КонецЕсли