#Область ОбработчикиСобытий 
 
&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
  	СтандартнаяОбработка = Ложь;
    
    СтруктураРасшифровки = Новый Структура("Задача, Пользователь");  
	ПолучитьДанныеРасшифровкиСоСтруктурой(Расшифровка, СтруктураРасшифровки);
	
	СписокКнопок = Новый СписокЗначений;
	СписокКнопок.Добавить(КодВозвратаДиалога.Да, "Добавить балл");
	СписокКнопок.Добавить(КодВозвратаДиалога.Нет, "Убавить балл");
	СписокКнопок.Добавить(КодВозвратаДиалога.Отмена, "Ничего");  
	
	СтруктураРасшифровки = Новый Структура("СтруктураРасшифровки", СтруктураРасшифровки);	
	Оповещение = Новый ОписаниеОповещения("РезультатОбработкаРасшифровкиЗавершение",
		ЭтотОбъект, СтруктураРасшифровки);  
		
	ПоказатьВопрос(Оповещение, "Что делаем?", СписокКнопок);
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатОбработкаРасшифровкиЗавершение(РезультатВопроса, ДополнительныеПараметры1) Экспорт
	
	СтруктураРасшифровки = ДополнительныеПараметры1.СтруктураРасшифровки;
	
	Ответ = РезультатВопроса;
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ДобавитьБаллПоЗадаче(СтруктураРасшифровки, 1);
		СкомпоноватьРезультат(РежимКомпоновкиРезультата.Непосредственно);
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		ДобавитьБаллПоЗадаче(СтруктураРасшифровки, -1);
		СкомпоноватьРезультат(РежимКомпоновкиРезультата.Непосредственно);
	Иначе
		Возврат;
	КонецЕсли;

КонецПроцедуры   

#КонецОбласти  

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ДобавитьБаллПоЗадаче(СтруктураРасшифровки, СколькоБаллов)

	ДобавитьБаллПоЗадачеНаСервере(СтруктураРасшифровки, СколькоБаллов);	
	
КонецПроцедуры
    
&НаСервере
Процедура ДобавитьБаллПоЗадачеНаСервере(СтруктураРасшифровки, СколькоБаллов)

	МенеджерЗаписи = РегистрыСведений.ИТК_РезультатыТестирования.СоздатьМенеджерЗаписи(); 
	МенеджерЗаписи.Задача = СтруктураРасшифровки.Задача;
	МенеджерЗаписи.Пользователь = СтруктураРасшифровки.Пользователь;
	МенеджерЗаписи.Прочитать(); 
	
	МенеджерЗаписи.КоличествоБаллов = МенеджерЗаписи.КоличествоБаллов + СколькоБаллов;
	МенеджерЗаписи.Записать(Истина);
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьСтруктуруВозврата(ПолеИлиГруппировка, СтруктураПолей) 

	МассивРодителей = ПолеИлиГруппировка.ПолучитьРодителей();
	Для Каждого Стр Из МассивРодителей Цикл
		Если ТипЗнч(Стр) = Тип("ЭлементРасшифровкиКомпоновкиДанныхГруппировка") Тогда 
			ПолучитьСтруктуруВозврата(Стр, СтруктураПолей);
		ИначеЕсли ТипЗнч(Стр) = Тип("ЭлементРасшифровкиКомпоновкиДанныхПоля") Тогда
			ПоляГруппировки = Стр.ПолучитьПоля();
			Для Каждого гСтр Из ПоляГруппировки Цикл
				Если СтруктураПолей.Свойство(гСтр.Поле) Тогда
					Если Не ЗначениеЗаполнено(СтруктураПолей[гСтр.Поле]) Тогда
						СтруктураПолей[гСтр.Поле] = гСтр.Значение;
					КонецЕсли;
				КонецЕсли;
				ПолучитьСтруктуруВозврата(Стр, СтруктураПолей);
			КонецЦикла;
		Иначе
			Возврат;
		КонецЕсли;
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеРасшифровкиСоСтруктурой(Расшифровка, СтруктураВозврата) 
	
	Данные = ПолучитьИзВременногоХранилища(ДанныеРасшифровки); 
	ПолучитьСтруктуруВозврата(Данные.Элементы[Расшифровка], СтруктураВозврата);  
	
КонецФункции 

#КонецОбласти